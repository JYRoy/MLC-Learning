# Emitting Basic MLIR & ODS Framework使用

- [Emitting Basic MLIR \& ODS Framework使用](#emitting-basic-mlir--ods-framework使用)
  - [Interfacing with MLIR](#interfacing-with-mlir)
  - [生成Toy IR](#生成toy-ir)
  - [Using the Operation Definition Specification(ODS) Framework](#using-the-operation-definition-specificationods-framework)
    - [Defining a Toy Dialect](#defining-a-toy-dialect)
    - [Defining Toy Operations](#defining-toy-operations)
    - [TableGen生成C++代码](#tablegen生成c代码)

## Interfacing with MLIR

MLIR被设计为完全可拓展的框架，没有封闭的属性集、operation和类型。支持通过Dialects来拓展。Dialects为唯一namespace下的抽象提供了一种分组机制。更好理解的说法我认为是Dialects用来定义各种IR，每个IR在独立的namespace下。

在MLIR中，`Operations`是核心单元，可以有特定于程序的语义，来表示所有LLVM中的IR数据结构：instructions、globals、modules等。

以前面用例中的`transpose` operation为例子：

```python
%t_tensor = "toy.transpose"(%tensor) {inplace = true} : (tensor<2x3xf64>) -> tensor<3x2xf64> loc("example/file/path":12:1)
```

- `%t_tensor`
  - operation s返回值的名字。在SSA框架下，一个operation可以返回0或者多个结果。但是Toy语言中限制为一个返回值。这个值不会在内存中追踪。
- `"toy.transpose"`
  - operation的名字。是一个唯一的字符串，`.`前面是dialect的namespace，后面是`transpose`操作的名字。
- `(%tensor)`
  - operation的输入参数列表
- `{inplace = true}`
  - operation的属性列表。
- `(tensor<2x3xf64>) -> tensor<3x2xf64>`
  - operation的输入参数类型到输出类型的映射。
- `loc("example/file/path":12:1)`
  - 源文件中的代码位置和行数。在LLVM中，只有debug模型才有这些信息。但是在MLIR中，这是必须的。一些API会依赖它们。原因是，如果一个operation被替换为另一个，它还要插入到原本的位置上。

## 生成Toy IR

对于这个toy的例子

```python
def multiply_transpose(a, b) {
  return transpose(a) * transpose(b);
}

def main() {
  var a<2, 3> = [[1, 2, 3], [4, 5, 6]];
  var b<2, 3> = [1, 2, 3, 4, 5, 6];
  var c = multiply_transpose(a, b);
  var d = multiply_transpose(b, a);
  print(d);
}
```

使用下面的命令，可以直接将toy代码生成mlir代码

```shell
./toyc-ch2 ../../mlir/test/Examples/Toy/Ch2/codegen.toy -emit=mlir -mlir-print-debuginfo
```

能够看到，除了原本代码中表面上看到的`toy.mul`、`toy.transpose`、`toy.generic_call`、`toy.print`还出现了一系列的`toy.reshape`、`toy.constant`等，但总之所有的operations的namespace都是toy。

```python
module {
  toy.func @multiply_transpose(%arg0: tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":4:1), %arg1: tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":4:1)) -> tensor<*xf64> {
    %0 = toy.transpose(%arg0 : tensor<*xf64>) to tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":5:10)
    %1 = toy.transpose(%arg1 : tensor<*xf64>) to tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":5:25)
    %2 = toy.mul %0, %1 : tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":5:25)
    toy.return %2 : tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":5:3)
  } loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":4:1)
  toy.func @main() {
    %0 = toy.constant dense<[[1.000000e+00, 2.000000e+00, 3.000000e+00], [4.000000e+00, 5.000000e+00, 6.000000e+00]]> : tensor<2x3xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":9:17)
    %1 = toy.reshape(%0 : tensor<2x3xf64>) to tensor<2x3xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":9:3)
    %2 = toy.constant dense<[1.000000e+00, 2.000000e+00, 3.000000e+00, 4.000000e+00, 5.000000e+00, 6.000000e+00]> : tensor<6xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":10:17)
    %3 = toy.reshape(%2 : tensor<6xf64>) to tensor<2x3xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":10:3)
    %4 = toy.generic_call @multiply_transpose(%1, %3) : (tensor<2x3xf64>, tensor<2x3xf64>) -> tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":11:11)
    %5 = toy.generic_call @multiply_transpose(%3, %1) : (tensor<2x3xf64>, tensor<2x3xf64>) -> tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":12:11)
    toy.print %5 : tensor<*xf64> loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":13:3)
    toy.return loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":8:1)
  } loc("../../mlir/test/Examples/Toy/Ch2/codegen.toy":8:1)
} loc(unknown)
```

## Using the Operation Definition Specification(ODS) Framework

MLIR提供了一种声明式的方式来支持定义dialect和operations，被称为ODS框架。我们可以将声明写在TableGen中，然后在编译时利用mlir的工具链生成C++代码。

下面是使用ODS框架来定义、生成toy dialect和operations的过程。

### Defining a Toy Dialect

按照前面说的，dialect对Toy语言进行了建模，我们在生成的mlir中可以看到所有的指令都是又toy语言的operations构成的。

因此，在实现的角度上，首要的就是先将Toy Dialect给定义出来，才能进行high-level的analysis和transformation。

定义这个dialect的时候是通过TableGen规范来定义在`mlir/examples/toy/Ch2/include/toy/Ops.td`中的。

```c++
def Toy_Dialect : Dialect {
  let name = "toy";
  let cppNamespace = "::mlir::toy";
}
```

然后，构造一个`Toy_Op`类代表toy dialect下所有operations的基类，所有的operations都要继承自这个类。

```c++
// Base class for toy dialect operations. This operation inherits from the base
// `Op` class in OpBase.td, and provides:
//   * The parent dialect of the operation.
//   * The mnemonic for the operation, or the name without the dialect prefix.
//   * A list of traits for the operation.
class Toy_Op<string mnemonic, list<Trait> traits = []> :
    Op<Toy_Dialect, mnemonic, traits>;
```

### Defining Toy Operations

现在有了toy dialect之后，可以对它包含的operations进行定义。

以Transpose为例子，能看到它确实是继承自`Toy_Op`类的

```c++
def TransposeOp : Toy_Op<"transpose"> {
  let summary = "transpose operation";

  let arguments = (ins F64Tensor:$input);
  let results = (outs F64Tensor);

  let assemblyFormat = [{
    `(` $input `:` type($input) `)` attr-dict `to` type(results)
  }];

  // Allow building a TransposeOp with from the input operand.
  let builders = [
    OpBuilder<(ins "Value":$input)>
  ];

  // Invoke a static verify method to verify this transpose operation.
  let hasVerifier = 1;
}
```

具体看operation中的细节。

- `summary`
  - 和`description`一起会自动生成operation的markdown文档
- `arguments`
  - operation的参数，ins代表input输入参数
  - `F64Tensor`：float 64的tensor类型
  - `$input`：参数的名字是input
- `results`
  - operation的输出，outs代表outputs输出参数
  - `F64Tensor`输出时float 64的tensor类型
- `hasVerifier`
  - ODS框架会自动生成验证逻辑
  - 但这并不是必须的，会在=1时才执行
  - 自动生成时重写verify方法，生成input、result的类型和shape的判断
- `builders`
  - ODS框架自动生成根据输入参数构建operation的build方法
  - build方法在`builder.create<ConstantOp>(...)`时被调用
  - 自动生成时重写build方法，使用`OpBuilder`来构建类型和参数，添加到`mlir::OperationState`对象中

### TableGen生成C++代码

使用`-gen-dialect-defs`参数执行mlir-tblgen命令

```shell
${build_root}/bin/mlir-tblgen -gen-dialect-defs ${mlir_src_root}/examples/toy/Ch2/include/toy/Ops.td -I ${mlir_src_root}/include/
```

可生成如下的输出

```c++
/*===- TableGen'erated file -------------------------------------*- C++ -*-===*\
|*                                                                            *|
|* Dialect Declarations                                                       *|
|*                                                                            *|
|* Automatically generated file, do not edit!                                 *|
|* From: Ops.td                                                               *|
|*                                                                            *|
\*===----------------------------------------------------------------------===*/

namespace mlir {
namespace toy {

class ToyDialect : public ::mlir::Dialect {
  explicit ToyDialect(::mlir::MLIRContext *context);

  void initialize();
  friend class ::mlir::MLIRContext;
public:
  ~ToyDialect() override;
  static constexpr ::llvm::StringLiteral getDialectNamespace() {
    return ::llvm::StringLiteral("toy");
  }
};
} // namespace toy
} // namespace mlir
```

使用`-gen-op-defs`生成operation的c++代码

```shell
${build_root}/bin/mlir-tblgen -gen-op-defs ${mlir_src_root}/examples/toy/Ch2/include/toy/Ops.td -I ${mlir_src_root}/include/
```

在官方实例中，则使用CMakeList.txt来生成对应的声明代码和定义代码，使用 mlir-tblgen 搭配 -gen-op-decls 和 -gen-op-defs 选项，生成 Ops.h.inc 声明代码和 Ops.cpp.inc 定义代码，将两者作为构建工具链 toyc-ch2 的代码依赖。

```txt
set(LLVM_TARGET_DEFINITIONS Ops.td)
mlir_tablegen(Ops.h.inc -gen-op-decls)
mlir_tablegen(Ops.cpp.inc -gen-op-defs)
mlir_tablegen(Dialect.h.inc -gen-dialect-decls)
mlir_tablegen(Dialect.cpp.inc -gen-dialect-defs)
add_public_tablegen_target(ToyCh2OpsIncGen)
```